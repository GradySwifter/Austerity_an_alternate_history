title: Presidential Election
subtitle: Hindenburg runs for re-election...
view-if: year = 1932 and (month = 4 or month = 5) and presidential_election_seen = 0
frequency: 1000
max-visits: 1
new-page: true
on-arrival: braun_campaign = 0; hitler_support_hindenburg = 0; presidential_election_seen = 1; nsdap_candidate = "Hitler" if hitler_deported == 0 else nsdap_candidate = "Göring";
tags: event
go-to: election_algorithm
set-jump: pres_election

@pres_election

The presidential election is coming up soon. [? if z_in_government = 0 : While the position of Chancellor is typically more important, recent governments have depended on the president's emergency decree powers, making the office vital. ?] [? if hitler_deported==0 : The Nazis will run Hitler, while the KPD will run Thälmann. A victory for Hitler would mean the end of parliamentary democracy, and the beginning of an era of unspeakable horrors. ?][? if hitler_deported==1 : Since Hitler has been deported, the Nazis will run Hermann Göring in his stead. He is widely seen as a proxy for Hitler, who, despite his legal persecution, retains a popular base who wishes to see him returned to Germany. Meanwhile, the KPD will run Ernst Thälmann. ?]

# if hitler is deported, who would nsdap run? Duesterberg? or Goering?
# Goering runs, essentially as a proxy for Hitler, who is now stateless.

Many are calling for us to support the incumbent Hindenburg, despite his well-known reactionary tendencies. Is he truly the best bulwark against Hitler's ascension? On the other hand, many in our party are calling for Otto Braun to run again, but he is reluctant.

[? if kpd_relation >= 50 : Another strange possibilty is an united front with the Communists, either to support Thälmann or our own candidate. ?]

- @support_hindenburg: Support Hindenburg.
- @support_marx: Run Wilhelm Marx of the Zentrum.
- @support_braun: Support Otto Braun of the SPD.

@support_hindenburg
subtitle: This is the default option.

Despite his known reactionary nature, Hindenburg has at least been keeping the Nazis at bay. Thus, we must support him.

- @final_election: The final vote...

@support_marx
subtitle: This will cost us 2 resources.
choose-if: resources >= 2
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 2; marx_campaign = 1
go-to: marx_campaign

It takes effort to run a quite unexpected campaign. First, Marx has to be convinced to run. He was on the verge of retirement from politics altogether, but we were somehow able to persuade him to run for the highest office in Germany.

The miniscule remnants of the German People's Party will likely support us. Who else will? Can we convince the SPD to support Marx? Perhaps even the DNVP?


@support_braun
choose-if: right_strength < left_strength
unavailable-subtitle: [? if right_strength >= left_strength : Our right-leaning faction is able to dissuade the party from supporting this arrangement. ?]

Supporting Braun is rather unpopular in the right of our party, even if the SPD decides to accept our support. Are we sure?

- @support_braun_2: Yes, we will support Braun, despite the right's pleas against it.
- @support_marx: Support Marx instead.
- @support_hindenburg: Support Hindenburg instead.

@support_braun_2
on-arrival: z_support_braun = 1; right_dissent += 15; center_dissent += 10; labor_dissent += 5; dvp_relation -= 5

Fine. We have thrown our support to Otto Braun of the SPD.

- @final_election: The final vote...

@marx_campaign

- @convince_spd: Convince the Social Democratic Party.
- @convince_dnvp: Convince the DNVP.
- @convince_minors: Try to convince the minor parties.
- @final_election: The final vote...

@convince_spd
choose-if: z_votes >= spd_votes+10
unavailable-subtitle: The SPD knows that they are vastly more popular than us; they do not see a reason to support Marx, especially after his past governments.
on-arrival: {!
Q.spd_support_marx = 0;
if (Q.spd_relation >= 55) {
    Q.spd_support_marx = 1;
} else if (Q.spd_relation <= 35) {
    Q.spd_support_marx = 0;
} else {
    Q.spd_support_marx = 2;
}
!}
go-to: spd_failed if spd_support_marx = 0 or (spd_support_marx == 2 and historical_mode); spd_success if spd_support_marx = 1; z_partial if z_support_braun = 2 and not historical_mode
max-visits: 1

We remind the SPD leaders of their support for Marx in the 1925 Presidential election, and try to convince them that a Zentrum president is what is needed to unify Germany in these dark times. In addition, we prey on their fear of excessively concentrated power by reminding them that Braun is already a very influential figure.

@spd_success
go-to: marx_campaign

The SPD is convinced! They will be part of the Marx coalition.

@spd_partial

The Center are not satisfied with our arguments, but they are open to being convinced. They are willing to run a joint campaign if we front the resources.

- @pay_spd: Give them the resources they need.
- @marx_campaign: Forget about the Center.

@pay_spd
choose-if: resources >= 3
subtitle: -3 resources
unavailable-subtitle: Not enough resources (need 3)
on-arrival: resources -= 3; spd_support_braun = 1
go-to: marx_campaign

With our resources, the SPD are convinced to support Marx over Braun.

@spd_failed
go-to: braun_campaign

Unfortunately, we fail to turn them away from Braun.

@convince_dnvp
choose-if: z_votes >= dnvp_votes
unavailable-subtitle: The DNVP knows that they are more popular than us; they do not see a reason to support our candidate.
max-visits: 1

The KPD is supporting President Hindenburg. [? if dnvp_relation >= 50 : However, they are willing to be convinced to drop their campaign and support marx; how do we convince them? ?][? if dnvp_relation < 50 : They do not seem willing to be convinced. ?]

- @dnvp_resources: Use our resources.
- @dnvp_relations: Use our good relations.
- @marx_campaign: Forget about the DNVP.

@dnvp_resources
view-if: dnvp_relation >= 50 and not historical_mode
choose-if: resources >= 3
subtitle: -3 resources
unavailable-subtitle: Not enough resources (need 3).
on-arrival: resources -= 3; dnvp_support_marx = 1
go-to: marx_campaign

The DNVP now endorses Marx in the presidential election! We have asked them to do this subtly, as to not alienate our left-leaning allies, but subtlety is not the DNVP's forte. 

@dnvp_relations
view-if: dnvp_relation >= 60
unavailable-subtitle: Our relations are not good enough.
on-arrival: dnvp_support_marx = 1
go-to: marx_campaign

The DNVP now endorses Marx in the presidential election! We ask them to keep their support low-key, so as to not alienate our left allies.




@final_election
on-arrival: election_round = 0
go-to: election_algorithm
set-jump: post_election


@post_election
on-arrival: {!
Q.marx_votes = 0;
Q.hindenburg_votes = 0;
Q.hitler_votes = 0;
Q.thalmann_votes = 0;
Q.braun_votes = 0;

// initial phase:
Q.hitler_votes += Q.nsdap_votes;
Q.hindenburg_votes += Q.other_votes;
Q.hindenburg_votes += Q.dnvp_votes;
Q.braun_votes += Q.spd_votes;
Q.braun_votes += Q.ddp_votes;
    if (Q.z_support_braun && Q.z_support_braun == 1) {
        Q.braun_votes += Q.z_votes;
    } else {
        Q.hindenburg_votes += Q.z_votes;
    }
    if (Q.kpd_support_braun) {
        Q.braun_votes += Q.kpd_votes;
    } else {
        Q.thalmann_votes += Q.kpd_votes;
    }
    if (Q.dvp_support_braun) {
        Q.braun_votes += Q.dvp_votes;
    } else {
        Q.hindenburg_votes += Q.dvp_votes;
    }
} else if (Q.spd_support_thalmann) {
    Q.thalmann_votes += Q.kpd_votes;
    Q.thalmann_votes += Q.spd_votes;
    Q.hindenburg_votes += Q.z_votes;
    Q.hindenburg_votes += Q.ddp_votes;
    Q.hindenburg_votes += Q.dvp_votes;
} else {
    Q.hindenburg_votes += Q.spd_votes;
    Q.hindenburg_votes += Q.z_votes;
    Q.hindenburg_votes += Q.ddp_votes;
    Q.hindenburg_votes += Q.dvp_votes;
    Q.thalmann_votes += Q.kpd_votes;
}
if (Q.hitler_support_hindenburg) {
    Q.hitler_votes -= Q.nsdap_votes;
    Q.hindenburg_votes += Q.nsdap_votes;
}

if (Q.hindenburg_to_braun_bonus) {
    Q.hindenburg_votes -= Q.hindenburg_to_braun_bonus;
    Q.braun_votes += Q.hindenburg_to_braun_bonus;
}
if (Q.thalmann_to_braun_bonus) {
    Q.thalmann_votes -= Q.thalmann_to_braun_bonus;
    Q.braun_votes += Q.thalmann_to_braun_bonus;
}

// check for majority
Q.hindenburg_majority = (Q.hindenburg_votes >= 50);
Q.thalmann_majority = (Q.thalmann_votes >= 50);
Q.hitler_majority = (Q.hitler_votes >= 50);
Q.braun_majority = (Q.braun_votes >= 50);
Q.any_majority = Q.hindenburg_majority | Q.thalmann_majority | Q.hitler_majority | Q.braun_majority;

// check for plurality
Q.hindenburg_plurality = (Q.hindenburg_votes >= Q.hitler_votes && Q.hindenburg_votes >= Q.braun_votes && Q.hindenburg_votes >= Q.thalmann_votes);
Q.thalmann_plurality = (Q.thalmann_votes >= Q.hindenburg_votes && Q.thalmann_votes >= Q.braun_votes && Q.thalmann_votes >= Q.hitler_votes);
Q.hitler_plurality = (Q.hitler_votes >= Q.hindenburg_votes && Q.hitler_votes >= Q.braun_votes && Q.hitler_votes >= Q.thalmann_votes);
Q.braun_plurality = (Q.braun_votes >= Q.hindenburg_votes && Q.braun_votes >= Q.hitler_votes && Q.braun_votes >= Q.thalmann_votes);
// If no candidate gets above 50%, then there's a second round. In the second round, a plurality wins. 
Q.election_round += 1;
!}
go-to: hindenburg_wins if hindenburg_majority == 1; thalmann_wins if thalmann_majority == 1; hitler_wins if hitler_majority == 1; braun_wins if braun_majority == 1; no_majority_round1 if any_majority == 0 and election_round == 1; round2_results if election_round >= 2

Results:

Hindenburg - [+ hindenburg_votes +]%

[+ nsdap_candidate +] - [+ hitler_votes +]%

[? if kpd_support_braun == 0 : Thälmann - [+ thalmann_votes +]% ?]

[? if braun_campaign > 0 : Braun - [+ braun_votes +]% ?]

@no_majority_round1
on-arrival: hitler_support_hindenburg = 1 if (braun_plurality or thalmann_plurality)
go-to: round2_menu

There is no majority in this round. In the second round, any candidate who wins a plurality will win overall. What should we do?

[? if braun_plurality or thalmann_plurality : In order to consolidate the vote for the Right, and to prevent a socialist from becoming president, [+ nsdap_candidate +] is no longer running in the second round, and has switched his endorsement to Hindenburg. ?]


@round2_menu

- @round2_convince_z: Convince the Center to switch their support to us.
- @round2_convince_kpd: Convince the KPD to switch their support to us. 
- @round2_hindenburg: Switch our support to Hindenburg.
- @round2_thalmann: Switch our support to Thälmann of the KPD.
- @round2_vote: Let the second round voting begin.


@round2_convince_z
view-if: braun_campaign > 0 and z_support_braun != 1 
choose-if: spd_votes >= z_votes
unavailable-subtitle: The Center knows that they are more popular than us; they do not see a reason to support our candidate.
on-arrival: {!
Q.z_support_braun = 0;

if (Q.z_relation >= 55) {
    Q.z_support_braun = 1;
    if (Q.hitler_support_hindenburg) {
        Q.hindenburg_to_braun_bonus += Math.round(Q.other_votes/2);
    }
} else if (Q.z_relation >= 50 && Q.hitler_support_hindenburg) {
    Q.z_support_braun = 1;
    Q.hindenburg_to_braun_bonus += Math.round(Q.other_votes/2);
} else if (Q.z_relation <= 35) {
    Q.z_support_braun = 0;
} else {
    Q.z_support_braun = 2;
}
!}
go-to: r2_z_failed if z_support_braun = 0 or (z_support_braun = 2 and historical_mode); r2_z_success if z_support_braun = 1; r2_z_partial if z_support_braun = 2 and not historical_mode
max-visits: 1

@r2_z_success
go-to: round2_menu

[? if hitler_support_hindenburg : The Center's opinion is affected by the Nazis' support of Hindenburg, and is willing to turn away from him. ?] The Center are convinced! They will be part of the Braun coalition. [? if hitler_support_hindenburg : In addition, they will also bring in some minor parties who will support us. ?]

@r2_z_partial

The Center are not satisfied with our arguments, but they are open to being convinced[? if hitler_support_hindenburg: , especially given the Nazis' support for Hindenburg. ?]. They are willing to run a joint campaign if we front the resources.

- @r2_pay_z: Give them the resources they need.
- @round2_menu: Forget about the Center.

@r2_pay_z
choose-if: resources >= 2
subtitle: -2 resources
unavailable-subtitle: Not enough resources (need 2)
on-arrival: resources -= 2; z_support_braun = 1
go-to: round2_menu

With our resources, the Center are convinced to support Braun over Hindenburg.

@r2_z_failed
go-to: round2_menu

Unfortunately, we fail to turn them away from Hindenburg.




@round2_convince_kpd
choose-if: spd_votes >= kpd_votes
unavailable-subtitle: The KPD knows that they are more popular than us; they do not see a reason to support our candidate.
view-if: braun_campaign > 0 and kpd_support_braun == 0 
max-visits: 1

The KPD is committed to supporting Ernst Thälmann. [? if kpd_relation >= 45 : However, the Nazi-Hindenburg coalition worries them enough that they are willing to be convinced to drop their campaign and support Braun. ?][? if kpd_relation < 45 : They do not seem willing to be convinced. ?]

- @r2_kpd_resources: Use our resources.
- @r2_kpd_relations: Use our good relations.
- @round2_menu: Forget about the KPD.

@r2_kpd_resources
view-if: kpd_relation >= 45 and not historical_mode
choose-if: resources >= 3
subtitle: -3 resources
unavailable-subtitle: Not enough resources (need 3).
on-arrival: resources -= 3; kpd_support_braun = 1
go-to: round2_menu

The KPD now endorses Braun in the presidential election! We ask them to keep their support low-key, so as to not alienate the bourgeois voters.

@r2_kpd_relations
view-if: kpd_relation >= 60
unavailable-subtitle: Our relations are not good enough.
on-arrival: kpd_support_braun = 1
go-to: round2_menu

The KPD now endorses Braun in the presidential election! We ask them to keep their support low-key, so as to not alienate the bourgeois voters. 

@round2_thalmann
view-if: spd_support_thalmann == 0 and kpd_support_braun == 0
choose-if: kpd_relation >= 50 and left_strength > reformist_strength 
unavailable-subtitle: [? if kpd_relation < 50 : We do not have good enough relations with the Communists, so they are not even willing to accept our support. ?] [? if reformist_strength >= left_strength : The reformist faction is able to dissuade the party from supporting this bizarre arrangement. ?]
max-visits: 1

Supporting Thälmann is rather unpopular in our party, even if the Communists accept our support. Are we sure?

- @round2_thalmann_2: Yes, we will support Thälmann, despite the reformists' pleas against it.
- @round2_menu: Never mind.

@round2_thalmann_2
on-arrival: braun_campaign = 0; spd_support_thalmann = 1; reformist_dissent += 15; center_dissent += 10; labor_dissent += 5; neorevisionist_dissent += 10; catholics_spd -= 5; workers_spd -= 3; kpd_relation += 10; communist_coalition += 2; z_relation -= 15; dvp_relation -= 15
on-departure: z_support_braun = 0; kpd_support_braun = 0;

Fine. We have thrown our support to Chairman Thälmann of the KPD.

[? if z_support_braun : The Center Party is confused at our capricious behavior, and have switched their support to Hindenburg. ?]

- @round2_vote: Let the second round voting begin.

@round2_hindenburg
view-if: braun_campaign > 0 or spd_support_thalmann > 0
on-arrival: braun_campaign = 0; spd_support_thalmann = 0

We have switched our support to Hindenburg.

- @round2_vote: Let the second round voting begin.

@round2_vote
go-to: election_algorithm
set-jump: post_election

@round2_results
go-to: hindenburg_wins if hindenburg_plurality == 1; thalmann_wins if thalmann_plurality == 1; braun_wins if braun_plurality == 1; hitler_wins if hitler_plurality == 1


@hindenburg_wins
on-arrival: president = "Hindenburg"

As expected, the incumbent Hindenburg wins the presidency. Let's hope he continues to keep the Nazis out of power.

@thalmann_wins
on-arrival: president = "Thälmann"; kpd_relation = 80;

Ernst Thälmann of the KPD has won the presidency. Immediately, the far-right launches a coup attempt against the government. We have no choice but to unite with the KPD against the fascists...

- @civil_war: This is civil war.

@hitler_wins
on-arrival: president = nsdap_candidate; chancellor = "Hitler"; chancellor_party = "NSDAP"

[? if nsdap_candidate == "Hitler" : Adolf Hitler of the NSDAP has won the presidency. He wastes no time in consolidating power. ?]
[? if nsdap_candidate != "Hitler" : The NSDAP candidate [+ president +] has won. The first action President [+ president +] takes is inviting Hitler back into Germany, and appointing him chancellor. The Nazis waste no time in consolidating power. ?]

- @hitler_takes_power: What do we do now?


@braun_wins
on-arrival: president = "Braun"; reformist_dissent -= 15; center_dissent -= 15; labor_dissent -= 15; left_dissent -= 5; workers_spd += 8*(1-dissent); old_middle_spd += 5*(1-dissent); new_middle_spd += 5*(1-dissent); rural_spd += 5*(1-dissent); unemployed_spd += 5*(1-dissent);

Otto Braun of the SPD has won the presidency! This is a moment of celebration for our party, and his victory is able to bolster our support.
